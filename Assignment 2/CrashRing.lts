const N = 3
range Node_ids = 0..N-1

set Messages = {token, claim[Node_ids]}

NODE(I=0) 	= 	IDLE,
IDLE 		= 	( rcv.token -> LEADER
				| snd.claim[I] -> ELIGIBLE
				| crash -> CRASHED
				| rcv.claim[j:Node_ids] ->
					if (j < I) then (snd.claim[j] -> IDLE)
					else IDLE
				),
LEADER 		= 	(open->close->snd.token->IDLE
				| snd.token->IDLE
				),
CRASHED		=	( idle -> CRASHED
				| rcv.token -> snd.token -> CRASHED
				| rcv.claim[j:Node_ids] ->
					if (j == I) then CRASHED
					else (snd.claim[I] -> CRASHED)
				),
ELIGIBLE	=	( snd.claim[I] -> ELIGIBLE
				| rcv.claim[I] -> LEADER
				| rcv.claim[j:Node_ids] ->
					//if (j == I) then LEADER
					if (j<I) then (snd.claim[j] -> IDLE)
					else ELIGIBLE
				| rcv.token -> LEADER
				| crash -> CRASHED
				) + {snd.claim[Node_ids]}.


CHANNEL	= (token.snd -> token.rcv -> CHANNEL | claim.snd[i:Node_ids] -> claim.rcv[i] -> CHANNEL).


||CRASHNRING = ([j:Node_ids]:NODE(j) || [i:Node_ids]:CHANNEL)
						 		/  {[i:Node_ids].snd.claim[j:Node_ids]/[i].claim.snd[j],
										[i:Node_ids].rcv.claim[j:Node_ids]/[(i+N-1)%N].claim.rcv[j],
										[i:Node_ids].snd.token/[i].token.snd,
										[i:Node_ids].rcv.token/[(i+N-1)%N].token.rcv}.

fluent CRITICAL[i:Node_ids] = <{[i].open}, {[i].close}>
assert MUTEX = forall[i:Node_ids] forall[j:Node_ids] (rigid(i!=j) -> []!(CRITICAL[i] && CRITICAL[j] ))
fluent FIRSTCLAIM[i:Node_ids] = <[i].snd.claim[i], [i].rcv.claim[i]>
assert NODUPCLAIM = forall[i:Node_ids] (FIRSTCLAIM[i] -> X (! FIRSTCLAIM[i]))
assert NODUP_MUTEX = (NODUPCLAIM -> MUTEX)

assert ALLFAIR = [] <> [Node_ids].open

assert NOCRASH = [] (! [Node_ids].crash)
assert FAIR = [] <> [Node_ids].open
assert NOCRASH_FAIR = (NOCRASH -> FAIR)
